label splashscreen:
    return

label before_main_menu:
    return

label start:
    scene bg campus
    show screen hud

    $ mc_name = renpy.input("주인공 이름은? (미입력 시 기본값:하진)", length=8) or mc_name
    $ mc_name = mc_name.strip()
    show mc_think at left
    n "서울시 동작구. 한 여름의 캠퍼스."
    n "입대 영장을 받은 [mc_name]은(는) 입대 전 마지막 방학을 보내고 있다."

    m "...드디어 끝나가네. 이 여름방학도..."
    
    # 튜토리얼/초기 선택
    n "입대 전 목표를 정하자."
    menu:
        "마음을 단단히. 감정 정리 & 다짐":
            $ resolve += 10
            $ met_coach = True
            jump meet_coach_first
        "현실 챙기기. 공부/돈/운동":
            $ study += 5
            $ money += 10
            $ fitness += 5
            jump day_loop

label meet_coach_first:
    scene bg room
    show coach_neutral at left
    coach "오늘부터 짧게라도 마음을 정리하는 시간을 가져보자."
    coach "'무엇을 통제할 수 있고, 무엇을 놓아줘야 하는가'를 매일 점검해."
    $ counselor_trust += 1
    hide coach_neutral
    jump day_loop

# ====== DAY LOOP ======
label day_loop:
    if days_left <= 0:
        jump check_endings

    # 아침 슬롯
    call morning_slot
    # 낮 슬롯
    call afternoon_slot
    # 밤 슬롯
    call night_slot

    # 랜덤 이벤트(하루 마감 전)
    $ roll = random_(100)
    if roll <= RANDOM_EVENT_CHANCE:
        call random_event

    # 밤 휴식 효과 (약간의 자연 회복)
    $ stress = clamp(stress - 1, 0, 100)

    # 시간 경과
    $ day += 1
    $ days_left -= 1

    jump day_loop

# ====== SLOT MENUS ======
label morning_slot:
    scene bg home
    n "아침. 무엇을 할까?"
    $ t= random_(100)
    if t<=60:
        menu:
            "도서관: 공부 +3, 스트레스 +1":
                show mc_think at left
                $ study = clamp(study + 3, 0, 100)
                $ stress = clamp(stress + 1, 0, 100)
                m "아침부터 집중 잘 됐다."
                hide mc_think
            "조깅: 피트니스 +3, 스트레스 -2":
                scene bg gym
                show mc_happy at left
                $ fitness = clamp(fitness + 3, 0, 100)
                $ stress = clamp(stress - 2, 0, 100)
                m "땀 좀 흘리니 머리가 맑아졌다."
                hide mc_happy
            "아르바이트: 돈 +10, 스트레스 +2":
                scene bg cafe
                show mc_neutral at left
                $ money = clamp(money + 10, 0, 999)
                $ stress = clamp(stress + 2, 0, 100)
                m "바쁜 아침. 그래도 벌 땐 벌어야지."
                hide mc_neutral
            "감정정리(코치): 다짐 +4, 스트레스 -1":
                show coach_happy at left
                $ resolve = clamp(resolve + 4, 0, 100)
                $ stress = clamp(stress - 1, 0, 100)
                $ counselor_trust += 1
                $ met_coach = True
                coach "어제의 감정에서 오늘로 가져올 건 무엇일까?"
                hide coach_happy
            "연락하기 (확률 이벤트)":
                jump morning_contact_roll
        return
    elif t<=80: # 20% 확률 지수 ai
        scene bg campus
        $ renpy.log("== AI: jisu / morning_contact ==")
        call ai_turn("jisu", "morning_contact", side="left")
        return
    else: #20% 확률 전여친 ai
        scene bg campus
        $ renpy.log("== AI: sua(ex) / morning_contact ==")
        call ai_turn("ex", "morning_contact", side="left")
        
#연락하기 (확률 이벤트)
label morning_contact_roll:
    # 1~100 사이 주사위
    $ r = random_(100)
    if r <= 40:
        # 친구 진수: 빠른 답장 (45%)
        scene bg cafe
        show jin_happy at right
        jin "피방 ㄱㄱ"
        $ social = clamp(social + 3, 0, 100)
        $ stress = clamp(stress - 1, 0, 100)
        m "뭐, 짜피 할 것도 없고. 그려그려. ㄱㄱ"
        jin "ㄱㄱ"
        hide jin_happy
    
    elif r<=45:
        # 여사친 지수: 쾌활하고 밝은 연락이 온다 (5%)
        scene bg campus
        show jisu_sad at left
        new_girl_1 "[mc_name]! 너 오늘 시간 돼? 나 코딩하다가 모르는게 있어서 ㅠㅠ 나 도와줄 수 있어?"
        menu:
            "수락한다":
                show mc_happy at right
                $ money = clamp(money -5, 0, 999)
                $ social = clamp(social +7, 0, 100)
                $ new_girl_1_affection +=1
                m "응, 그래 내가 도와줄게!"
                hide mc_happy
            "거절한다":
                show mc_sad at right
                hide jisu_sad
                show jisu_think at left
                $ money = clamp(money +0, 0, 999)
                $ social = clamp(social +0, 0, 100)
                m "미안, 내가 바빠서..."
                new_girl_1 "웅... 그래? 그럼 언제 시간 돼? 시간 맞춰볼까?"
                hide mc_sad
                hide jisu_think
                menu:
                    "수락한다":
                        show mc_think at right
                        show jisu_happy at left
                        $ money = clamp(money -5, 0, 999)
                        $ social = clamp(social +7, 0, 100)
                        $ new_girl_1_affection +=1
                        m "뭐, 나 내일은 시간 되는데. 내일 점심 어때?"
                        new_girl_1 "웅 조아! 그럼 내일 점심에 보자~!"
                        hide mc_think
                        hide jisu_happy
                    "거절한다":
                        show mc_sad at right
                        show jisu_sad at left
                        $ money = clamp(money +0, 0, 999)
                        $ social = clamp(social +0, 0, 100)
                        $ new_girl_1_affection -=7
                        m "미안, 내가 요즘 바쁘고 힘든 일이 있어서 못 도와줄거 같아."
                        new_girl_1 "음... 그래 그럼 어쩔 수 없지 ㅠㅠ 알겠어."
                        hide mc_sad
                        hide jisu_sad

    elif r <= 65:
        # 가족: 안부 통화 (20%)
        scene bg home
        show mom_neutral at left
        mom "아침은 먹고 다니니? 뭔 일 없지? 좀 글고 일찍 자고!"
        $ social = clamp(social + 2, 0, 100)
        $ resolve = clamp(resolve + 1, 0, 100)
        m "네 엄마. 그럴게요."
        hide mom_neutral

    elif r <= 80:
        # 전애인: 연락이 불편하다. (15%)
        scene bg campus
        m "수아야, 잘 지내..?"
        m "그동안 정말 미안하고 고마웠어. 짧게라도 너랑 이야기하고 싶은데 안될까?"
        show ex_sad at left
        ex "미안, 더이상 연락 안하면 좋겠어."
        hide ex_sad
        $ stress = clamp(stress + 5, 0, 100)
        $ ex_affection = clamp(ex_affection - 5, 0, 100)
        menu:
            "계속 연락을 이어간다":
                $ s = random_(10)
                if s <= 7:
                    #미련으로 인한 더 큰 단절.
                    $ stress = clamp(stress +5, 0, 100)
                    $ ex_affection = clamp(ex_affection - 5, 0, 100)
                    m "수아야, 내가 그동안 잘못했어. 미안해. 내가 더 노력할게 제발..."
                    show ex_sad at left
                    ex "이제 그만해. 다신 연락하지 마."
                    n "쓸쓸한 적막만이 이 공간을 차갑게 채웠다."
                else:
                    #미련과 미련
                    $ stress = clamp(stress -1, 0, 100)
                    $ ex_affection = clamp(ex_affection + 3, 0, 100)
                    m "수아야... 나도 알아. 이미 끝난 거라는 거."
                    m "근데 가끔은... 그냥 네가 잘 지내는지만 알고 싶어. 난 너가 행복하길 바래. 너 웃는 모습이 가장 예뻐."
                    show ex_neutral at left
                    ex "……."
                    ex "나도 네가 행복했으면 좋겠어. 너가 싫은게 아냐. 더는 우리 얘기로 아프지 않길 바래."
                    ex "하지만 지금은... 서로에게 거리를 두는 게 맞는 것 같아."
                    hide ex_neutral
                    n "짧지만 깊은 여운이 남았다. 아직 완전히 지워지지 않은 마음이, 공기 속에 흩날렸다."
            "더이상 연락하지 않는다":
                show mc_sad at left
                n "어딘가 마음 한 켠이 아려온다."
                m "군대에서 고생하고 시간 지나면... 시간이... 해결해 줄까?"
                n "그는 알고있었다. 시간은 그저 무뎌지게 할 뿐 상처를 치료해주진 못한다는 것을..."
                hide mc_sad

    elif r <= 92:
        # 전애인: 짧은 안부(루트 플래그 ON) (12%)
        scene bg campus
        show ex_neutral at left
        ex "잘 지내지? 바쁘지 않으면… 건강만 챙겨."
        $ route_ex = True
        $ ex_affection = clamp(ex_affection + 2, 0, 100)
        $ stress = clamp(stress - 1, 0, 100)
        m "(짧은 말이지만, 마음이 조금 정리되는 느낌이 들었다.)"
        hide ex_neutral

    else:
        # 번외: 알바 추가콜/오입력 문자 등 가벼운 이벤트 (8%)
        scene bg cafe
        n "가게에서 오전 단기 알바 제안 문자가 왔다."
        menu:
            "수락한다 (돈 +8, 스트레스 +1)":
                show mc_happy at left
                $ money = clamp(money + 8, 0, 999)
                $ stress = clamp(stress + 1, 0, 100)
                m "이럴 때 한 푼이라도 더 벌어두자."
                hide mc_happy
            "거절한다 (스트레스 -1)":
                show mc_neutral at left
                $ stress = clamp(stress - 1, 0, 100)
                m "오늘은 내 컨디션을 우선하자."
                hide mc_neutral
    return

label afternoon_slot:
    scene bg campus
    n "낮. 햇살이 강하다."
    menu:
        "팀플/수업: 공부 +3, 관계 +1, 스트레스 +1":
            show mc_think at left
            $ study = clamp(study + 3, 0, 100)
            $ social = clamp(social + 1, 0, 100)
            $ stress = clamp(stress + 1, 0, 100)
            m "프로젝트도 언젠간 끝나겠지..."
            hide mc_think
        "체력단련: 피트니스 +4, 다짐 +1":
            scene bg gym
            show mc_neutral at left
            $ fitness = clamp(fitness + 4, 0, 100)
            $ resolve = clamp(resolve + 1, 0, 100)
            m "몸이 버티면 마음도 버틴다."
            hide mc_neutral
        "친구와 점심: 관계 +3, 돈 -5":
            scene bg cafe
            $ social = clamp(social + 3, 0, 100)
            $ money = clamp(money - 5, 0, 999)
            show jin_think at left
            jin "야, 너 표정 좋아졌다? 준비 잘 되고 있어?"
            m "어... 나름?"
            hide jin_think
        "감정정리(코치) 세션: 다짐 +3, 스트레스 -1":
            $ resolve = clamp(resolve + 3, 0, 100)
            $ stress = clamp(stress - 1, 0, 100)
            $ counselor_trust += 1
            show coach_think at left
            coach "나는 왜 사는걸까? 핵심가치 세 가지를 적어보자. 생각이 정리가 될거야."
            hide coach_think
        "낮잠: 의지 -3, 스트레스 -3":
            show mc_happy at left
            $ resolve = clamp(resolve -3, 0, 100)
            $ stress = clamp(stress -3, 0, 100)
            m "zzz...(현실을 잠시 회피할 순 있으나 무기력해지고 수면욕만 강해진다.)"
            hide mc_happy
    return

label night_slot:
    scene bg room
    n "밤. 하루를 마무리할 시간."
    menu:
        "복습/과제: 공부 +2, 스트레스 +1":
            $ study = clamp(study + 2, 0, 100)
            $ stress = clamp(stress + 1, 0, 100)
            show mc_think at left
            m "조금만 더...근데.. 나는 무얼 위해 공부하는 거지?"
            hide mc_think
        "독서/저널링: 다짐 +2, 스트레스 -2":
            $ resolve = clamp(resolve + 2, 0, 100)
            $ stress = clamp(stress - 2, 0, 100)
            show mc_think at left
            m "글로 쓰며 마음을 정리했다. 인생을 어떻게 마무리 해야 의미있고 아름다울까?"
            hide mc_think
        "가족 대화: 관계 +2, 돈 0":
            $ social = clamp(social + 2, 0, 100)
            show mom_think at left
            mom "요즘 무슨 일 없지? 입대가 얼마 안 남아서 마음이 좀 그러니?"
            m "엄마, 괜찮아요. 무슨 일 없어요! 그동안 감사했습니다."
            hide mom_think
        "휴식: 스트레스 -3":
            $ stress = clamp(stress - 3, 0, 100)
            show mc_happy at left
            m "아무것도 안 하는 용기. 잠시 쉬는 것도 꼭 필요해!"
            hide mc_happy
        "연락하기":
            jump night_contact_roll
    return

label night_contact_roll:
    # 1~100 사이 주사위
    $ r = random_(100)
    if r <= 40:
        # 친구 진수: 빠른 답장 (45%)
        scene bg cafe
        show jin_neutral at left
        jin "뭐하냐?"
        $ social = clamp(social + 3, 0, 100)
        $ stress = clamp(stress + 1, 0, 100)
        m "그냥 있는데 왜?"
        jin "노래방 ㄱㄱ"
        scene bg karaoke
        n "둘은 신나게 노래방에서 노래를 불렀다. 어딘가 마음은 쓰렸지만 말이다."
        hide jin_neutral
    
    elif r<=45:
        # 여사친 지수: 쾌활하고 밝은 연락이 온다 (5%)
        scene bg campus
        show jisu_sad at left
        #question 하나 뽑기
        $ _q = choose_(JISU_QS)
        $ _face = _q["face"]
        $ _tag = "jisu" + _face
        show expression _tag at left

        new_girl_1 "[mc_name]! 너 오늘 시간 돼? 나 코딩하다가 모르는게 있어서 ㅠㅠ 나 도와줄 수 있어?"
        menu:
            "수락한다":
                $ money = clamp(money -5, 0, 999)
                $ social = clamp(social +7, 0, 100)
                $ new_girl_1_affection +=1
                m "응, 그래 내가 도와줄게!"
                new_girl_1 "음... "
                new_girl_1 "[_q['text']]"
                # 답변 방식 선택 (자유입력 or 카테고리 선택)
                menu:
                    "직접 답변 입력하기":
                        $ user_ans = renpy.input("네가 생각하는 해결책은? (핵심 키워드 위주로 적어줘)", length=80)
                        $ score, tip, cat = eval_text_answer(_q["id"], user_ans)
                    "선택지로 답하기(요점만)":
                        menu:
                            "경계/초기화/예외(배열 범위, visited, null 등)":
                                $ score, tip = eval_choice_answer(_q["id"], "bounds")
                            "알고리즘/자료구조(정렬, 두 포인터, JOIN/정규식 등)":
                                $ score, tip = eval_choice_answer(_q["id"], "algo")
                            "환경/설정(CORS, 깃 충돌, 빌드/경로 등)":
                                $ score, tip = eval_choice_answer(_q["id"], "env")
                # 점수에 따른 반응/스탯 변화
                if score >= 2:
                    show jisu happy at left
                    new_girl_1 "오… 그거였넹! 확실히 이해됐엉. 고마웡!!"
                elif score == 1:
                    show jisu neutral at left
                    new_girl_1 "힌트가 좀 감 잡히는 듯! 방향 잡아볼게."
                else:
                    show jisu sad at left
                    new_girl_1 "흐음… 아직 잘 모르겠다. 같이 더 보자!"
                # 해설 한 줄 (내레이션으로)
                n "{i}[tip]{/i}"
                # 효과 적용
                $ _eff = apply_effects_by_score(score)
                $ social = clamp(social + _eff.get("social", 0), 0, 100)
                $ resolve = clamp(resolve + _eff.get("resolve", 0), 0, 100)
                $ stress = clamp(stress + _eff.get("stress", 0), 0, 100)
                $ new_girl_1_affection = clamp(new_girl_1_affection + _eff.get("newgirl", 0), 0, 100)
                return
            "거절한다":
                show jisu_sad at left
                show mc_think at right
                $ money = clamp(money +0, 0, 999)
                $ social = clamp(social +0, 0, 100)
                m "미안, 내가 바빠서..."
                new_girl_1 "웅... 그래? 그럼 언제 시간 돼? 시간 맞춰볼까?"
                hide jisu_sad
                hide mc_think
                menu:
                    "수락한다":
                        show mc_think at right
                        show jisu_happy at left
                        $ money = clamp(money -5, 0, 999)
                        $ social = clamp(social +7, 0, 100)
                        $ new_girl_1_affection +=1
                        m "뭐, 나 내일은 시간 되는데. 내일 점심 어때?"
                        new_girl_1 "웅 조아! 그럼 내일 점심에 보자~!"
                        hide mc_think
                        hide jisu_happy
                    "거절한다":
                        show mc_sad at right
                        show jisu_sad at left
                        $ money = clamp(money +0, 0, 999)
                        $ social = clamp(social +0, 0, 100)
                        $ new_girl_1_affection -=7
                        m "미안, 내가 요즘 바쁘고 힘든 일이 있어서 못 도와줄거 같아."
                        new_girl_1 "음... 그래 그럼 어쩔 수 없지 ㅠㅠ 알겠어."
                        hide mc_sad
                        hide jisu_sad

    elif r <= 65:
        # 가족: 안부 통화 (20%)
        scene bg home
        show mom_think at left
        mom "뭔 일 없지? 좀 빨리빨리 움직이고! 네 물건 잘 챙기고!"
        $ social = clamp(social + 2, 0, 100)
        $ resolve = clamp(resolve + 1, 0, 100)
        m "네 엄마. 그럴게요."
        hide mom_think

    elif r <= 80:
        # 전애인: 연락이 불편하다. (15%)
        scene bg campus
        show mc_sad at right
        m "수아야, 잘 지내..?"
        m "그동안 정말 미안하고 고마웠어. 짧게라도 너랑 이야기하고 싶은데 안될까?"
        show ex_neutral at left
        ex "미안, 더이상 연락 안하면 좋겠어."
        $ stress = clamp(stress + 5, 0, 100)
        $ ex_affection = clamp(ex_affection - 5, 0, 100)
        hide ex_neutral
        hide mc_sad
        menu:
            "계속 연락을 이어간다":
                $ s = random_(10)
                if s <= 7:
                    #미련으로 인한 더 큰 단절.
                    $ stress = clamp(stress +5, 0, 100)
                    $ ex_affection = clamp(ex_affection - 5, 0, 100)
                    show mc_sad at left
                    m "수아야, 내가 그동안 잘못했어. 미안해. 내가 더 노력할게 제발..."
                    show ex_neutral at right
                    ex "이제 그만해. 다신 연락하지 마."
                    n "쓸쓸한 적막만이 이 공간을 차갑게 채웠다."
                    hide mc_sad
                    hide ex_neutral
                else:
                    #미련과 미련
                    $ stress = clamp(stress -1, 0, 100)
                    $ ex_affection = clamp(ex_affection + 3, 0, 100)
                    show mc_sad at left
                    m "수아야... 나도 알아. 이미 끝난 거라는 거."
                    m "근데 가끔은... 그냥 네가 잘 지내는지만 알고 싶어. 난 너가 행복하길 바래. 너 웃는 모습이 가장 예뻐."
                    show ex_sad at right
                    ex "……."
                    ex "나도 네가 행복했으면 좋겠어. 너가 싫은게 아냐. 더는 우리 얘기로 아프지 않길 바래."
                    ex "하지만 지금은... 서로에게 거리를 두는 게 맞는 것 같아."
                    n "짧지만 깊은 여운이 남았다. 아직 완전히 지워지지 않은 마음이, 공기 속에 흩날렸다."
                    hide mc_sad
                    hide ex_sad
            "연락을 그만둔다":
                # 분위기 롤
                $ s2 = random_(100)
                if s2 <= 40:
                # 1) 성숙한 작별
                    $ stress = clamp(stress - 2, 0, 100)
                    $ resolve = clamp(resolve + 2, 0, 100)
                    show mc_neutral at left
                    m "…알겠어. 더는 연락하지 않을게. 그동안 고마웠어. 건강히 지내."
                    hide mc_neutral
                    n "보내지 못한 말들이 목구멍까지 차올랐지만, [mc_name]은(는) 천천히 폰을 내려놓았다."
                elif s2 <= 80:
                # 2) 씁쓸한 포기
                    $ stress = clamp(stress - 1, 0, 100)
                    $ resolve = clamp(resolve + 1, 0, 100)
                    show mc_sad at left
                    m "(이제… 놓자.)"
                    hide mc_sad
                    n "대화창을 닫는 소리가 방에 또각 하고 울렸다. 공기가 조금 가벼워졌다."
                else:
                # 3) 냉정한 단절(무말)
                    $ stress = clamp(stress - 3, 0, 100)
                    $ resolve = clamp(resolve + 1, 0, 100)
                    $ ex_affection = clamp(ex_affection - 2, 0, 100)
                    n "[mc_name]은(는) 알림을 끄고 대화방을 정리했다. 마지막 말은 남기지 않았다."
                # 레어: 전애인이 짧게 답장(15%)
                $ p = random_(100)
                if p <= 15:
                    show ex_neutral at right
                    ex "…응, 잘 지내."
                    hide ex_neutral
                    n "짧은 한 줄이 묘하게 오래 남았다."
                return
    elif r <= 92:
        # 전애인: 짧은 안부(루트 플래그 ON) (12%)
        scene bg campus
        show ex_neutral at left
        ex "잘 지내지? 바쁘지 않으면… 건강만 챙겨."
        $ route_ex = True
        $ ex_affection = clamp(ex_affection + 2, 0, 100)
        $ stress = clamp(stress - 1, 0, 100)
        m "(짧은 말이지만, 마음이 조금 정리되는 느낌이 들었다.)"
        hide ex_neutral
    else:
        # 번외: 알바 추가콜/오입력 문자 등 가벼운 이벤트 (8%)
        scene bg cafe
        n "발신표시제한으로 전화가 온다."
        menu:
            "수락한다":
                $ money = clamp(money + 8, 0, 999)
                $ stress = clamp(stress + 1, 0, 100)
                m "여보세요?"
                n "???: 책 사게 돈 10만원만 보내줘."
                show mc_happy at left
                m "누구세요? 저 아세요? ㅋㅋ"
                show sis_think at right
                sis "아 좀! 오빠 나 진짜 급하게 필요해서 그래.. 제발 응?"
                m "에휴. 근데 왜 대체 발신표시제한으로 전화 거냐? ㅋㅋ 뭐가 찔려서."
                sis "오빠! 진짜 제발. 급해서 그래. 그리고 며칠 전에 부모님한테 용돈 받아서 또 말하기가 좀 그래서..."
                hide mc_happy
                hide sis_think
                menu:
                    "돈을 보내준다":
                        $ money = clamp(money - 10, 0, 999)
                        show mc_neutral at left
                        m "그래. 수능 얼마 안남았으니까 공부 열심히 하고."
                        show sis_happy at right
                        sis "응응! 오빠 진짜 고마워! 빠이~"
                        hide mc_neutral
                        hide sis_happy
                    "돈을 보내주지 않는다":
                        $ money = clamp(money - 0, 0, 999)
                        $ stress = clamp(stress - 3, 0, 100)
                        $ social = clamp(social -3, 0, 100)
                        show mc_neutral at left
                        m "꺼져. 수험생이 무슨 돈 쓸 때가 그리 많다고.."
                        show sis_neutral at right
                        sis "아 오빠 좀! 하여간 도움이 안되요 흥!"
                        hide mc_neutral
                        hide sis_neutral

            "거절한다":
                $ stress = clamp(stress - 1, 0, 100)
                m "모르는 연락은 받지 말자."
    return

# ====== RANDOM EVENTS ======
label random_event:
    $ r = random_(5)
    if r == 1 and not route_ex:
        # 잠재적 전애인 루트 오픈
        scene bg campus
        show ex_neutral at left
        show mc_neutral at right
        n "복도에서 낯익은 뒷모습."
        ex "... [mc_name]?"
        m "어... 잘 지냈어?"
        $ route_ex = True
        $ ex_affection += 2
        hide ex_neutral
        hide mc_neutral
        return
    elif r == 2:
        scene bg cafe
        show jin_happy at left
        show mc_think at right
        jin "이번 주에 야구장 가자."
        menu:
            "간다 (관계+2, 피트니스+1, 공부-1)":
                $ social = clamp(social + 2, 0, 100)
                $ fitness = clamp(fitness + 1, 0, 100)
                $ study = clamp(study - 1, 0, 100)
            "패스한다 (공부+1, 관계-1)":
                $ study = clamp(study + 1, 0, 100)
                $ social = clamp(social - 1, 0, 100)
        return
    elif r == 3 and money < 20:
        scene bg home
        sis "오빠... 용돈 조금만 줄까?ㅋㅋ"
        m "됐어. 오빠가 알아서 할게. 고3이라 힘들텐데 공부나 열심히 혀라."
        $ resolve = clamp(resolve + 1, 0, 100)
        return
    elif r == 4 and met_coach:
        scene bg room
        show coach_sad at left
        coach "오늘의 체크인: 통제할 수 없는 것 하나를 떠올리고, 놓아주자."
        $ stress = clamp(stress - 2, 0, 100)
        $ resolve = clamp(resolve + 1, 0, 100)
        hide coach_sad
        return
    else:
        # 소소한 일상
        n "잔잔한 하루였다. 작은 평안이 스며든다."
        $ stress = clamp(stress - 1, 0, 100)
        return

#ai LLM 기반 스토리 및 상황 
label ai_turn(npc, scene_id, side="right"):
    # 1) 상태/메모리 수집 + LLM 호출
    python:
        state = get_game_state()
        mem = ai_memory[-8:]
        resp = ai.ask(npc, scene_id, mem, state)

    if "error" in resp:
        python:
            who = renpy.store.__dict__.get(npc, n)
            err = resp.get("error", "서버와 통신 실패")
        $ renpy.say(who, "(AI 서버 오류) " + err)
        return

    # 2) 스프라이트 표시 (별칭 as ai_face 사용)
    $ sprite = resp.get("sprite", "neutral")
    $ image_expr = f"{npc} {sprite}"
    if side == "right":
        show expression image_expr as ai_face at pos_right
    else:
        show expression image_expr as ai_face at pos_left

    # 3) 대사 출력
    python:
        who = renpy.store.__dict__.get(npc, n)
        line = resp.get("say", "...")
    $ renpy.say(who, line)

    # 4) 선택지 처리
    python:
        chs = resp.get("choices", [])
    if not chs:
        hide ai_face
        return

    python:
        idx = renpy.call_screen("ai_choices", chs)
        choice = chs[idx]

    # 5) 스탯 반영
    python:
        eff = choice.get("effects", {})
        stress = clamp(stress + eff.get("stress", 0), 0, 100)
        resolve = clamp(resolve + eff.get("resolve", 0), 0, 100)
        social = clamp(social + eff.get("social", 0), 0, 100)
        study = clamp(study + eff.get("study", 0), 0, 100)
        fitness = clamp(fitness + eff.get("fitness", 0), 0, 100)
        money = clamp(money + eff.get("money", 0), 0, 999)
        ex_affection = clamp(ex_affection + eff.get("ex_affection", 0), 0, 100)
        new_girl_1_affection = clamp(new_girl_1_affection + eff.get("jisu_affection", 0), 0, 100)

    # 6) 기억 업데이트
    python:
        ai_memory.append({"npc": npc, "say": line, "picked": choice.get("text", "")})

    hide ai_face

    # 7) 분기 점프
    python:
        if choice.get("next"):
            _next = choice["next"]
            renpy.jump(_next)
    return


# ====== ENDINGS ======
label check_endings:
    scene bg campus

    # 1) 특수 루트 우선
    if route_ex and ex_affection >= 65 and resolve >= 60 and stress <= 60:
        jump ending_peace_closure
    elif new_girl_1_affection >= 60 and resolve >= 60 and social >= 55 and study >= 80:
        jump ending_love_1_closure

    # 2) 일반 성취/소진 엔딩
    elif resolve >= 85 and stress <= 30 and fitness >= 70 and counselor_trust >= 4:
        jump ending_new_start
    elif study >= 85 and resolve >= 65:
        jump ending_focus_future
    elif stress >= 85:
        jump ending_burnout
    else:
        jump ending_bittersweet

label ending_new_start:
    show mc_neutral at left
    n "입대 전날 밤."
    m "두렵지만, 준비는 됐다. 내가 선택해온 날들이 나를 만든다."
    n "새벽 공기를 가르며, [mc_name]의 새로운 시작이 열린다."
    m "비록 내가 원한 입대시기도 아니고 내가 원하는 대로 된게 없지만 그래도 아직 나아갈 기회가 남았어!"
    n "굳게 다짐하는 [mc_name]. 내일 공군에 입대하게 된다."
    n "비록 힘들었던 순간이 많았지만, 결국 [mc_name]은 다 이겨냈고 새로운 시작을 할 수 있을 것이다."
    hide mc_neutral
    return

label ending_focus_future:
    show mc_think at left
    n "정리된 책상, 조용한 마음."
    m "돌아와서도 이어갈 공부와 길. 나는 나의 미래를 택한다. 그래도 하루하루 버티며 살아는 있어야지."
    hide mc_think
    return

label ending_burnout:
    show coach_sad at left
    n "과열된 엔진은 잠시 쉬어야 한다."
    coach "멈춤도 전략이야. 도움을 요청하는 건 용기야."
    n "[mc_name]은(는) 소주를 연거푸 마신다. 쓰라린 소주가 더이상 쓰지 않다."
    hide coach_sad
    return

label ending_peace_closure:
    show ex_neutral at left
    show mc_neutral at right
    ex "잘 지내. 서로 각자의 길에서."
    m "고마웠어. 그 모든 날들."
    n "한 페이지를 덮고, 더 단단해진 마음으로 내일을 맞는다."
    hide ex_neutral
    hide mc_neutral
    return

label ending_love_1_closure:
    show mc_happy at right
    show jisu_happy at left
    m "지수야, 내가 힘들 때도 즐거울 때에도 내 곁에 있어줘서 고마워!"
    new_girl_1 "ㅎㅎ 뭘.. 나도 너한테 고마워!"
    hide mc_happy
    show mc_think at right
    m "그래서 내가 곰곰히 생각해 봤는데..."
    hide mc_think
    hide jisu_happy
    menu:
        "고백한다":
            show mc_happy at right
            show jisu_happy at left
            m "지수야, 나 너 좋아해. 나랑 사귀자."
            n "지수의 얼굴이 붉어지며 눈웃음이 지어진다."
            new_girl_1 "웅! 조아! 오늘부터 1일!"
            new_girl_1 "군대에서도 꼭 연락하고! 내가 편지 꼭 쓸게! 사랑해~"
            hide mc_happy
            hide jisu_happy
        "고백하지 않는다":
            show mc_neutral at right
            show jisu_sad at left
            m "넌 정말 좋은 친구인거 같아! 앞으로도 우리 우정 변치 말자."
            n "지수의 표정이 미묘하게 흔들린다."
            new_girl_1 "친구..? 그..그래.."
            n "지수의 표정이 미묘하게 슬퍼보인다."
            n "[mc_name]또한 마음이 그리 썩 좋지 않았다. 불확실한 미래 때문에 군대 떄문에 그리고 자기 마음 때문에..."
            hide mc_neutral
            hide jisu_sad
    return

label ending_bittersweet:
    show mc_sad at left
    n "쓰라리다."
    m "삶이 더욱 흐릿해져간다."
    n "입대라는 경계 앞에서, [mc_name]은(는) 결국 운명에 순응한채 하루하루를 그저 버티며 슬퍼한다."
    hide mc_sad
    return
